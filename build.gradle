buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        repositories {
            maven {
                url "https://plugins.gradle.org/m2/"

            }
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.1.0"
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
repositories {
    jcenter()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }// Maybe needed for other mods in the future
    maven { url "https://maven.latmod.com/" } //FTB
    maven { url 'https://jitpack.io' }
    maven { url "https://nexus.scarsz.me/content/groups/public/"}
}
version = "1.2.4"
group = "de.erdbeerbaerlp.dcintegration" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "dcintegration"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: '20190719-1.14.3'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    // accessTransformer = file('build/resources/main/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                dcintegration {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                dcintegration {
                    source sourceSets.main
                }
            }
        }
    }
}
configurations {
    embed
    compile.extendsFrom(embed)
}
shadowJar {
    classifier = '1.15'
    configurations = [project.configurations.embed]
    relocate 'okhttp3', 'dcshadow.okhttp'
    relocate 'okio', 'dcshadow.okio'
    relocate 'club.minnced', 'dcshadow.club.minnced'
    relocate 'com.sun', 'dcshadow.com.sun'
    relocate 'com.iwebpp', 'dcshadow.com.iwebpp'
    relocate 'com.neovisionaries', 'dcshadow.com.neovisionaries'
    relocate 'gnu', 'dcshadow.gnu'
    relocate 'org.apache.commons.collections4', 'dcshadow.apache-collections4'
    relocate 'org.slf4j', 'dcshadow.org.slf4j'
    relocate 'org.json', 'dcshadow.org.json'
    relocate 'org.intellij', 'dcshadow.org.intellij'
    relocate 'org.jetbrains', 'dcshadow.org.jetbrains'
    relocate 'tomp2p', 'dcshadow.tomp2p'
    relocate 'com.fasterxml', 'dcshadow.com.fasterxml'
    relocate 'me.vankka', 'dcshadow.me.vankka'
    relocate 'net.kyori', 'dcshadow.net.kyori'
    relocate 'org.checkerframework', 'dcshadow.org.checkerframework'
}
dependencies {
    minecraft 'net.minecraftforge:forge:1.15.2-31.2.5'
    compile("org.spongepowered:mixin:0.8-SNAPSHOT") {
        transitive = false
    }
    embed("net.dv8tion:JDA:4.1.1_101")
    embed 'club.minnced:discord-webhooks:0.1.8'

    //Markdown <==> Discord conversion
    embed "me.vankka:MCDiscordReserializer:2.1.0"
    embed "me.vankka:SimpleAST:2.0.3"
    embed "net.kyori:text-api:3.0.0"
    embed "net.kyori:text-serializer-legacy:3.0.0"
    //deobfCompile "com.feed_the_beast.mods:FTBUtilities:5.+"
    //deobfCompile "com.feed_the_beast.mods:FTBLib:5.+"
    //deobfCompile 'com.github.upcraftlp:votifier:master-SNAPSHOT'
}
mixin {
    add sourceSets.main, 'mixins.dcintegration.refmap.json'
}
reobf {
    shadowJar {
        dependsOn tasks.createMcpToSrg
        mappings = tasks.createMcpToSrg.outputs.files.singleFile
    }
}

artifacts {
    archives tasks.shadowJar
}
jar {
    classifier = "1.15"
    manifest {
        attributes([
                'Maven-Artifact'          : "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp'               : System.currentTimeMillis(),
                "Specification-Title"     : "dcintegration",
                "Specification-Vendor"    : "dcintegration",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "dcintegration",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConnector': 'de.erdbeerbaerlp.dcintegration.DCMixinConnector'
        ])
    }
}
curseforge {
    // $GRADLE_USER_HOME/gradle.properties
    if (project.hasProperty('curseforge.apikey')) {
        apiKey = getProperty("curseforge.apikey")
        project {
            id = '324952'
            changelog = project.changelog
            releaseType = 'beta'
            addGameVersion '1.15.2'
                mainArtifact(jar) {
                displayName = "DiscordIntegration-$version (MC 1.15)"
            }
            relations {
                optionalDependency 'ftb-utilities'
                optionalDependency 'emojicord'
                optionalDependency 'voting'
                requiredDependency 'mixinbootstrap'
            }
            options {
                forgeGradleIntegration = false // defaults to true
            }
        }
    }
}
tasks.curseforge.dependsOn(build)