buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        repositories {
            maven {
                url "https://plugins.gradle.org/m2/"

            }
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}

//Gradle Plugins
plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id 'java'
    id "com.modrinth.minotaur" version "1.2.1"
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'


//Build variables
version = "2.3.5"
group = "de.erdbeerbaerlp.dcintegration"
archivesBaseName = "dcintegration-forge"



//Gradle Repositories
repositories {
    maven { url 'https://jitpack.io' }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven { url "https://nexus.vankka.dev/repository/maven-public/" }
    maven { url "https://repository.dev.gotan.io/repository/gotan.os/" }
    maven { url "https://repo.opencollab.dev/maven-snapshots" }
    maven { url "https://repo.opencollab.dev/maven-releases" }
    maven {
        name = 'sonatype-oss'
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven{
        name = 'howaner'
        url = "https://repo.howaner.de/"
    }
}

//Forge
minecraft {
    mappings channel: 'snapshot', version: '20210309-1.16.5'
    runs {
        server {
            workingDirectory project.file('run')
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            mods {
                dcintegration {
                    source sourceSets.main
                }
            }
        }
    }
}


//ShadowJar
configurations {
    embed
    compileOnly.extendsFrom(embed)
}
task release{

}
shadowJar {
    classifier = '1.16'
    configurations = [project.configurations.embed]
}
reobf {
    shadowJar {
        dependsOn tasks.createMcpToSrg
        mappings = tasks.createMcpToSrg.outputs.files.singleFile
    }
}
artifacts {
    archives tasks.shadowJar
}

//Dependencies
dependencies {

    // =========  Common  =====================
    embed 'de.erdbeerbaerlp:DiscordIntegration-Core:11ef997b'
    //embed 'dcintegration-core:dcintegration-core-2.3.0'

    // =========  Minecraft Forge =============
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.66'
    //Mixin
    annotationProcessor 'org.spongepowered:mixin:0.8.2:processor'

    //Dynmap-API
    compileOnly('com.github.webbukkit:DynmapCoreAPI:2.5')
}


//Mixin
mixin {
    add sourceSets.main, 'mixins.dcintegration.refmap.json'
}

//Manifest attributes
jar {
    classifier = "1.17"
    manifest {
        attributes([
                'Maven-Artifact'          : "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp'               : System.currentTimeMillis(),
                "Specification-Title"     : "dcintegration",
                "Specification-Vendor"    : "dcintegration",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "dcintegration",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConnector'          : 'de.erdbeerbaerlp.dcintegration.forge.DCMixinConnector'
        ])
    }
}

//Curseforge publishing
curseforge {
    if (project.hasProperty('curseforge.apikey')) { // $GRADLE_USER_HOME/gradle.properties
        apiKey = getProperty("curseforge.apikey")
        project {
            id = '324952'
            changelog = project.changelog
            releaseType = project.releaseType.toLowerCase()
            addGameVersion '1.16.5'
            mainArtifact(jar) {
                displayName = "DiscordIntegration-Forge $version (MC 1.16.5)"
            }
            //relations {
            //}
        }
    }
}
import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload){

    if (project.hasProperty('modrinth.apikey')) { // $GRADLE_USER_HOME/gradle.properties
        token = getProperty("modrinth.apikey") // Use an environment property!
        projectId = 'rbJ7eS5V'
        versionNumber = version+"-1.16."
        versionName = "DiscordIntegration-Forge $version (MC 1.16.5)"
        uploadFile = shadowJar
        changelog = project.changelog
        addGameVersion('1.16.5')
        addLoader('forge')
        versionType = project.releaseType.toUpperCase()
    }
}

tasks.release.dependsOn(tasks.build, tasks.curseforge,tasks.publishModrinth)
