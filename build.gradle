buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        repositories {
            maven {
                url "https://plugins.gradle.org/m2/"

            }
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.32'
    }
}

//Gradle Plugins
plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id 'java'
    id "com.modrinth.minotaur" version "2.1.2"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'


//Build variables
version = "2.4.1"
group = "de.erdbeerbaerlp.dcintegration"
archivesBaseName = "dcintegration-forge"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

//Gradle Repositories
repositories {
    maven { url 'https://jitpack.io' }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven { url "https://nexus.vankka.dev/repository/maven-public/" }
    maven { url "https://repository.dev.gotan.io/repository/gotan.os/" }
    maven { url "https://repo.opencollab.dev/maven-snapshots" }
    maven { url "https://repo.opencollab.dev/maven-releases" }
    maven {
        name = 'sonatype-oss'
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    flatDir {
        dirs 'libs'
    }
    maven { url "https://repo.erdbeerbaerlp.de/repository/maven-public/" }
}

//Forge
minecraft {
    mappings channel: 'official', version: "${project.mcVersion}"
//    runs {
//        server {
//            workingDirectory project.file('run')
//            // Recommended logging data for a userdev environment
//            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
//            // Recommended logging level for the console
//            property 'forge.logging.console.level', 'debug'
//            mods {
//                dcintegration {
//                    source sourceSets.main
//                }
//            }
//        }
//    }
}


//ShadowJar
configurations {
    embed
    compileOnly.extendsFrom(embed)
}
task release{

}
shadowJar {
    archiveClassifier = "${project.mcVersion}"
    configurations = [project.configurations.embed]
    relocate 'org.slf4j', 'dcshadow.org.slf4j' //Required to get forge 1.17 running
}
reobf {
    shadowJar {
        dependsOn tasks.createMcpToSrg
        mappings = tasks.createMcpToSrg.outputs.files.singleFile
    }
}
artifacts {
    archives tasks.shadowJar
}
configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 5, 'minutes'
    }
}
//Dependencies
dependencies {

    // =========  Common  =====================
    embed('de.erdbeerbaerlp:dcintegration.common:2.4.1') {
        changing = true
    }
    //embed 'dcintegration-core:dcintegration-core-2.4.0'

    // =========  Minecraft Forge =============
    minecraft "net.minecraftforge:forge:${project.mcVersion}-40.0.24"

    //Mixin
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    //Dynmap-API
    compileOnly('com.github.webbukkit:DynmapCoreAPI:2.5')
}


//Mixin
mixin {
    add sourceSets.main, 'mixins.dcintegration.refmap.json'
    config 'mixins.dcintegration.json'
}

//Manifest attributes
jar {
    archiveClassifier = "${project.mcVersion}"
    manifest {
        attributes([
                'Maven-Artifact'          : "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp'               : System.currentTimeMillis(),
                "Specification-Title"     : "dcintegration",
                "Specification-Vendor"    : "dcintegration",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${project.version}",
                "Implementation-Vendor"   : "dcintegration",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConnector'          : 'de.erdbeerbaerlp.dcintegration.forge.DCMixinConnector'
        ])
    }
}

//Curseforge publishing
curseforge {
    if (project.hasProperty('curseforge.apikey')) { // $GRADLE_USER_HOME/gradle.properties
        apiKey = getProperty("curseforge.apikey")
        project {
            id = '324952'
            changelog = project.changelog
            releaseType = project.releaseType.toLowerCase()
            addGameVersion "${project.mcVersion}"
            mainArtifact(jar) {
                displayName = "DiscordIntegration-Forge $version (MC ${project.mcVersion})"
            }
            //relations {
            //}
        }
    }
}
modrinth{
    if (project.hasProperty('modrinth.apikey')) { // $GRADLE_USER_HOME/gradle.properties
        token = getProperty("modrinth.apikey") // Use an environment property!
        projectId = 'rbJ7eS5V'
        versionNumber = version+"-${project.mcVersion}"
        versionName = "DiscordIntegration-Forge $version (MC ${project.mcVersion})"
        uploadFile = shadowJar
        changelog = project.changelog
        gameVersions = ["${project.mcVersion}"]
        loaders = ['forge']
        versionType = project.releaseType.toUpperCase()
    }
}

tasks.release.dependsOn(tasks.build, tasks.curseforge,tasks.modrinth)
